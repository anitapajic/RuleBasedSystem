package cep;

import com.ftn.sbnz.model.events.TemperatureEvent
import java.util.Date;

declare Alarm
    @role(event)
    temperatureValue: double
    patientName: String
    patientId: int
end

rule "Generate Temperature Warning"
no-loop
when
    $e1 : TemperatureEvent($patientId: patientId, temperatureValue>38.5, $temperatureValue: temperatureValue, $patientName: patientName)
        Number(intValue >= 2) from accumulate(
            $e : TemperatureEvent(
                temperatureValue > 38.5,
                patientId == $patientId,
                patientName == $patientName
            )
            over window:time(12h),
            count(1)
        )
        not Alarm(patientId==$patientId)
    then
        insert(new Alarm($temperatureValue, $patientName, $patientId));
        System.out.println("Kreirano je upozorenje za visoku temperaturu za pacijenta: " + $patientName);
end


rule "Generate Emergency Intervention"
no-loop
when
    $alarm : Alarm($patientId: patientId, $temperatureValue: temperatureValue, $patientName: patientName)
    not TemperatureEvent(
        patientId == $patientId,
        temperatureValue < 38.5,
        this after[0s, 2h] $alarm
    )
then
    System.out.println("Potrebna je hitna intervencija za pacijenta: " + $patientName + " zbog visoke temperature od " + $temperatureValue);
end




